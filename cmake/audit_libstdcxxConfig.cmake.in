@PACKAGE_INIT@

include("${CMAKE_CURRENT_LIST_DIR}/audit_libstdcxx.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/system_libstdcxx.cmake")

# Have to clear and re-add this property, as it contains a workaround for the generator used
# `target_link_options` cannot be used as $ORIGIN escaping is not functional
# target_link_libraries appears to behave MUCH better when it passes $ORIGIN to the build system
# ALSO, it appears that support for $ORIGIN escaping in Ninja differs from Makefiles. Probably contributes to the above problem

if (TARGET link_audit_libstdcxx)
  if ( "${CMAKE_GENERATOR}" STREQUAL "Ninja" )
    set( _INTERFACE_LINK_LIBS_
      "$<$<BOOL:$<TARGET_PROPERTY:AUDIT_LIBRARIES>>:-Wl$<COMMA>--audit$<COMMA>'$<GENEX_EVAL:$<TARGET_PROPERTY:AUDIT_LIBRARIES>>'>"
      "$<$<NOT:$<BOOL:$<TARGET_PROPERTY:AUDIT_LIBRARIES>>>:-Wl$<COMMA>--audit$<COMMA>'\$$ORIGIN/$<PATH:RELATIVE_PATH,$<TARGET_FILE:audit_libstdcxx>,$<TARGET_PROPERTY:BINARY_DIR>>'>"
    )
    set_target_properties(link_audit_libstdcxx PROPERTIES INTERFACE_LINK_LIBRARIES
      "${_INTERFACE_LINK_LIBS_}"
    )
    unset(_INTERFACE_LINK_LIBS_)
  else()
    set( _INTERFACE_LINK_LIBS_
      "$<$<BOOL:$<TARGET_PROPERTY:AUDIT_LIBRARIES>>:-Wl$<COMMA>--audit$<COMMA>'$<GENEX_EVAL:$<TARGET_PROPERTY:AUDIT_LIBRARIES>>'>"
      "$<$<NOT:$<BOOL:$<TARGET_PROPERTY:AUDIT_LIBRARIES>>>:-Wl$<COMMA>--audit$<COMMA>'\$ORIGIN/$<PATH:RELATIVE_PATH,$<TARGET_FILE:audit_libstdcxx>,$<TARGET_PROPERTY:BINARY_DIR>>'>"
    )
    set_target_properties(link_audit_libstdcxx PROPERTIES INTERFACE_LINK_LIBRARIES
      "${_INTERFACE_LINK_LIBS_}"
    )
    unset(_INTERFACE_LINK_LIBS_)
  endif()
endif()
