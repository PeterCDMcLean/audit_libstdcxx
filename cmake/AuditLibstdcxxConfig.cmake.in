@PACKAGE_INIT@

include("${CMAKE_CURRENT_LIST_DIR}/AuditLibstdcxx.cmake")

# These two imported exectuable targets are not really executables
# But are used to save the paths to the scripts/templates in a 'target-ish' way
if (NOT TARGET AuditLibstdcxx::python_audit_libstdcxx)
  add_executable(AuditLibstdcxx::python_audit_libstdcxx IMPORTED GLOBAL)
  file(COPY "${CMAKE_CURRENT_LIST_DIR}/../pyaudit/audit_libstdcxx.py"
       DESTINATION "${CMAKE_CURRENT_BINARY_DIR}/pyaudit"
  )
  file(COPY "${CMAKE_CURRENT_LIST_DIR}/../pyaudit/elftools"
       DESTINATION "${CMAKE_CURRENT_BINARY_DIR}/pyaudit"
  )
  set_target_properties(AuditLibstdcxx::python_audit_libstdcxx PROPERTIES
    IMPORTED_LOCATION "${CMAKE_CURRENT_BINARY_DIR}/pyaudit/audit_libstdcxx.py"
  )
endif()

if (NOT TARGET AuditLibstdcxx::python_sitecustomize_template)
  add_executable(AuditLibstdcxx::python_sitecustomize_template IMPORTED GLOBAL)
  set_target_properties(AuditLibstdcxx::python_sitecustomize_template PROPERTIES
    IMPORTED_LOCATION "${CMAKE_CURRENT_LIST_DIR}/../pyaudit/sitecustomize.py.in"
  )
endif()

include("${CMAKE_CURRENT_LIST_DIR}/libstdcxx.cmake")

# Have to clear and re-add this property, as it contains a workaround for the generator used
# `target_link_options` cannot be used as $ORIGIN escaping is not functional
# target_link_libraries appears to behave MUCH better when it passes $ORIGIN to the build system
# ALSO, it appears that support for $ORIGIN escaping in Ninja differs from Makefiles. Probably contributes to the above problem

if (TARGET AuditLibstdcxx::link_audit_libstdcxx)
  if ( "${CMAKE_GENERATOR}" STREQUAL "Ninja" )
    set( _INTERFACE_LINK_LIBS_
      "$<$<LINK_LANG_AND_ID:CXX,GNU>:$<$<BOOL:$<TARGET_PROPERTY:AUDIT_LIBRARIES>>:-Wl$<COMMA>--audit$<COMMA>'$<GENEX_EVAL:$<TARGET_PROPERTY:AUDIT_LIBRARIES>>'>>"
      "$<$<LINK_LANG_AND_ID:CXX,GNU>:$<$<NOT:$<BOOL:$<TARGET_PROPERTY:AUDIT_LIBRARIES>>>:-Wl$<COMMA>--audit$<COMMA>'\$$ORIGIN/$<PATH:RELATIVE_PATH,$<TARGET_FILE:AuditLibstdcxx::audit_libstdcxx>,$<TARGET_PROPERTY:BINARY_DIR>>'>>"
    )
    set_target_properties(AuditLibstdcxx::link_audit_libstdcxx PROPERTIES INTERFACE_LINK_LIBRARIES
      "${_INTERFACE_LINK_LIBS_}"
    )
    unset(_INTERFACE_LINK_LIBS_)
  else()
    set( _INTERFACE_LINK_LIBS_
      "$<$<LINK_LANG_AND_ID:CXX,GNU>:$<$<BOOL:$<TARGET_PROPERTY:AUDIT_LIBRARIES>>:-Wl$<COMMA>--audit$<COMMA>'$<GENEX_EVAL:$<TARGET_PROPERTY:AUDIT_LIBRARIES>>'>>"
      "$<$<LINK_LANG_AND_ID:CXX,GNU>:$<$<NOT:$<BOOL:$<TARGET_PROPERTY:AUDIT_LIBRARIES>>>:-Wl$<COMMA>--audit$<COMMA>'\$ORIGIN/$<PATH:RELATIVE_PATH,$<TARGET_FILE:AuditLibstdcxx::audit_libstdcxx>,$<TARGET_PROPERTY:BINARY_DIR>>'>>"
    )
    set_target_properties(AuditLibstdcxx::link_audit_libstdcxx PROPERTIES INTERFACE_LINK_LIBRARIES
      "${_INTERFACE_LINK_LIBS_}"
    )
    unset(_INTERFACE_LINK_LIBS_)
  endif()
endif()